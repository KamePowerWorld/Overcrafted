plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
    id "io.freefair.lombok" version "6.6.1"
}

group = 'quarri6343'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven{
        name = 'codemc-snapshots'
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.org/repository/maven-public/")
    }
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    implementation 'dev.triumphteam:triumph-gui:3.1.4'
    implementation 'net.wesjd:anvilgui:1.6.3-SNAPSHOT'
    implementation 'de.tr7zw:item-nbt-api-plugin:2.11.1'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'dev.triumphteam.gui', 'quarri6343.overcrafted.gui'
    relocate 'net.wesjd.anvilgui', 'quarri6343.overcrafted.anvilgui'
    relocate 'de.tr7zw.nbtapi', 'quarri6343.overcrafted.nbtapi'
}

// Disable jar and replace with shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)

